buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'

apply plugin: 'maven-publish'

deobfMcMCP { failOnAtError = false }
deobfMcSRG { failOnAtError = false }

repositories {
    mavenCentral()
    maven { url = 'https://repo.spongepowered.org/maven' }
}

dependencies {
    compile project(":OrbisAPI")
    runtime project(":Phosphor")
}

allprojects {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

if (System.getenv("CI_JOB_ID") != null) {
    version = "${minecraft_version}-${mod_version}+build-${System.getenv("CI_JOB_ID")}"
} else {
    version = "${minecraft_version}-${mod_version}+snapshot-${System.currentTimeMillis()}"
}

group = "com.gildedgames"
archivesBaseName = "aether_ii"

sourceSets {
    api {
        java { srcDir "src/api/java" }
    }
}

jar {
    dependsOn(':OrbisAPI:build')
    dependsOn(':Phosphor:build')

    from sourceSets.api.output
    from sourceSets.main.output

    gradle.projectsEvaluated({
        manifest.mainAttributes(
                "ContainedDeps": "${project(":OrbisAPI").mod_name}-${project(":OrbisAPI").version}.jar " +
                        "${project(":Phosphor").mod_name}-${project(":Phosphor").version}-universal.jar",
                "FMLAT": "aether_at.cfg"
        )

        from("lib/OrbisAPI/build/libs") {
            include("${project(":OrbisAPI").mod_name}-${project(":OrbisAPI").version}.jar")
        }

        from("lib/Phosphor/build/libs") {
            include("${project(":Phosphor").mod_name}-${project(":Phosphor").version}-universal.jar")
        }
    })

    classifier = 'universal'

}

minecraft {
    version = "$minecraft_version-$forge_version"
    mappings = "$forge_mappings"

    runDir = "run"
}

minecraft {
    clientJvmArgs += "-Dfml.coreMods.load=me.jellysquid.mods.phosphor.core.PhosphorFMLLoadingPlugin"
    serverJvmArgs += "-Dfml.coreMods.load=me.jellysquid.mods.phosphor.core.PhosphorFMLLoadingPlugin"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }

    rename '(.+_at.cfg)', 'META-INF/$1'
}

idea {
    module {
        inheritOutputDirs = true
    }
}

// We disable this because Gradle will fail to build otherwise for some reason.
// See https://github.com/MinecraftForge/Srg2Source/issues/10
minecraft {
    makeObfSourceJar = false
}

task signJar(type: SignJar, dependsOn: reobfJar) {
    keyStore = System.getenv('SIGN_KEYSTORE')
    alias = System.getenv('SIGN_ALIAS')
    storePass = System.getenv('SIGN_STORE_PASS')
    keyPass = System.getenv('SIGN_KEY_PASS')

    inputFile = jar.archivePath
    outputFile = jar.archivePath
}

if (System.getenv("SIGN_BUILD") == 'true') {
    build.dependsOn signJar
}

task apiJar(type: Jar) {
    classifier = "api"

    from sourceSets.api.output
}

artifacts {
    archives jar
    archives apiJar
}

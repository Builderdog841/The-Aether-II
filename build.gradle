buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven-publish'

def isPhosphorSubmodule = findProject(":Phosphor") != null

repositories {
    mavenCentral()

    maven { url = 'https://repo.spongepowered.org/maven' }
    maven { url = 'https://files.gildedgames.com/maven' }
}

configurations {
    compileShaded
    compile.extendsFrom compileShaded

    runtimeShaded
    runtime.extendsFrom runtimeShaded

    shaded
    shaded.extendsFrom runtimeShaded, compileShaded
    shaded.exclude group: "org.spongepowered"
}

dependencies {
    compileShaded project(":OrbisAPI")

    if (isPhosphorSubmodule) {
        runtime project(':Phosphor')
    } else {
        runtime "me.jellysquid.mods:phosphor:${phosphor_version}:dev"
        shaded  "me.jellysquid.mods:phosphor:${phosphor_version}:universal"
    }
}

allprojects {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

if (System.getenv("CI_JOB_ID") != null) {
    version = "${minecraft_version}-${mod_version}+build${System.getenv("CI_JOB_ID")}"
} else {
    version = "${minecraft_version}-${mod_version}-SNAPSHOT"
}

group = "com.gildedgames"
archivesBaseName = "aether_ii"

sourceSets {
    api {
        java { srcDir "src/api/java" }
    }

    main {
        resources {
            exclude 'assets/**'
        }
    }
}

jar {
    from sourceSets.api.output
    from sourceSets.main.output

    gradle.projectsEvaluated({
        def artifacts = configurations.shaded.files
            .findAll { it.name.contains("-universal.jar") }

        from(artifacts)

        manifest.mainAttributes(
                "ContainedDeps": artifacts.collect { it.name }.join(' '),
                "FMLAT": "aether_at.cfg"
        )
    })

    classifier = "no-assets"
}

task reobfJarWithAssets(type: Jar) {
    from zipTree(jar.archivePath)

    into("assets") {
        from "src/main/resources/assets"
    }

    classifier "universal"
}

reobfJar.finalizedBy reobfJarWithAssets

minecraft {
    version = "$minecraft_version-$forge_version"
    mappings = "$forge_mappings"

    runDir = "run"

    clientJvmArgs = serverJvmArgs += "-Dfml.coreMods.load=me.jellysquid.mods.phosphor.core.PhosphorFMLLoadingPlugin"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        exclude 'assets/**'

        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
        exclude 'assets/**'
    }

    rename '(.+_at.cfg)', 'META-INF/$1'
}

idea {
    module {
        inheritOutputDirs = true
    }
}

// We disable this because Gradle will fail to build otherwise for some reason.
// See https://github.com/MinecraftForge/Srg2Source/issues/10
minecraft {
    makeObfSourceJar = false
}

task signJar(type: SignJar, dependsOn: reobfJar) {
    keyStore = System.getenv('SIGN_KEYSTORE')
    alias = System.getenv('SIGN_ALIAS')
    storePass = System.getenv('SIGN_STORE_PASS')
    keyPass = System.getenv('SIGN_KEY_PASS')

    inputFile = jar.archivePath
    outputFile = jar.archivePath
}

if (System.getenv("SIGN_BUILD") == 'true') {
    build.dependsOn signJar
}

task apiJar(type: Jar) {
    classifier = "api"

    from sourceSets.api.output
}

artifacts {
    archives jar
    archives apiJar
}

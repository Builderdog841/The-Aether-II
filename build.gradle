buildscript {
	repositories {
		jcenter()
		maven {
			name = "forge"
			url = "http://files.minecraftforge.net/maven"
		}
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.2'
		classpath 'org.ajoberstar:grgit:2.1.1'
	}
}

import org.ajoberstar.grgit.Grgit

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'maven-publish'

allprojects {
    repositories {
        maven {
            name = "maven-storage"
            url = "https://git.gildedgames.com/GildedGames/maven-storage/raw/master/"
        }
    }

    dependencies {
    	//compile files('libs/Orbis-1.12.2-1.0.20')
        compile 'com.gildedgames:orbis:1.12.2-1.0.22'
    }
}

ext.config = loadConfig(file('build.properties'))
ext.repo = Grgit.open()

allprojects {
	sourceCompatibility = 1.8
	targetCompatibility = 1.8
}

version = "${config.mod_version}"
group = "com.gildedgames"
archivesBaseName = "AetherII"

ext.ciVersion = System.getenv("BUILD_NUMBER")
ext.gitVersion = repo.head().abbreviatedId

if (ciVersion != null) {
	version = "${config.mod_version}.build-${ciVersion}"
} else {
	version = "${config.mod_version}.git-${gitVersion}"
}

sourceSets {
	api {
		java { srcDir "src/api/java" }
	}
}

jar {
	from sourceSets.api.output
	from sourceSets.main.output
}

minecraft {
	version = config.forge_version
	mappings = config.forge_mappings

	runDir = "run"
}

processResources {
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version

	from (sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'

		expand 'version': project.version, 'mcversion': project.minecraft.version
	}

	from (sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
}

idea {
	module {
		inheritOutputDirs = true
	}
}

// We disable this because Gradle will fail to build otherwise for some reason.
// See https://github.com/MinecraftForge/Srg2Source/issues/10
minecraft {
	makeObfSourceJar = false
}

static def loadConfig(File config) {
	def props = new Properties()
	config.withInputStream {
		stream -> props.load(stream)
	}
	return new ConfigSlurper().parse(props)
}

task signJar(type: SignJar, dependsOn: reobfJar) {
	if (project.hasProperty("aether_keystore")) {
		keyStore = project.aether_keystore
		alias = project.aether_keystore_alias
		storePass = project.aether_keystore_pass
		keyPass = project.aether_keystore_pass
		inputFile = jar.archivePath
		outputFile = jar.archivePath
	}
}

task apiJar(type: Jar) {
	classifier = "api"

	from sourceSets.api.output
}

artifacts {
	archives jar
	archives apiJar
}

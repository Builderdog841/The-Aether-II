buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven-publish'

repositories {
    mavenCentral()

    maven { url = 'https://repo.spongepowered.org/maven' }
    maven { url = 'https://repo.gildedgames.com/maven' }
}

configurations {
    shaded
    shaded.extendsFrom runtimeShaded, compileShaded
    shaded.exclude group: "org.spongepowered"
}

dependencies {
    runtime "me.jellysquid.mods:phosphor:${phosphor_version}:dev"
    shaded  "me.jellysquid.mods:phosphor:${phosphor_version}:universal"

    compile "com.gildedgames:orbis-lib:${orbis_version}:dev"
    shaded "com.gildedgames:orbis-lib:${orbis_version}:universal"
}

allprojects {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

if (System.getenv("BUILD_NUMBER") != null) {
    version = "${minecraft_version}-${mod_version}+build${System.getenv("BUILD_NUMBER")}"
} else {
    version = "${minecraft_version}-${mod_version}-SNAPSHOT"
}

group = "com.gildedgames"
archivesBaseName = "aether_ii"

sourceSets {
    api {
        java { srcDir "src/api/java" }
    }
}

// This is an ugly hack to make Phosphor appear before Orbis-Lib
// If we don't do this, then Forge will explode for some unknown reason and not be able to handle dependency resolution
def shadedJars = configurations.shaded.files
        .findAll { it.name.contains("-universal.jar") }
        .toList()
        .reverse()

def genericJarManifest = {
    attributes "ContainedDeps": shadedJars.collect { it.name }.join(' ')
    attributes "FMLAT": "aether_at.cfg"
}

jar {
    from sourceSets.api.output
    from sourceSets.main.output

    classifier "universal"

    from shadedJars

    manifest genericJarManifest
}

task apiJar(type: Jar) {
    classifier = "api"

    from sourceSets.api.output
}

task serverJar(type: Jar) {
    classifier = "server"

    from sourceSets.api.output
    from (sourceSets.main.output) {
        // Exclude client assets
        exclude 'assets/*/blockstates/**'
        exclude 'assets/*/sounds/**'
        exclude 'assets/*/textures/**'
        exclude 'assets/*/models/**'

        // Exclude client code
        exclude 'com/gildedgames/aether/client/**'
    }

    from shadedJars

    manifest genericJarManifest
}

minecraft {
    version = "$minecraft_version-$forge_version"
    mappings = "$forge_mappings"

    runDir = "run"

    def plugin = "me.jellysquid.mods.phosphor.core.PhosphorFMLLoadingPlugin"

    clientJvmArgs = serverJvmArgs += "-Dfml.coreMods.load=$plugin"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        exclude 'assets/**'

        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
        exclude 'assets/**'
    }
}

idea {
    module {
        inheritOutputDirs = true
    }
}

// We disable this because Gradle will fail to build otherwise for some reason.
// See https://github.com/MinecraftForge/Srg2Source/issues/10
minecraft {
    makeObfSourceJar = false
}

if (System.getenv("JARSIGN_SECRETS_FILE") != null) {
    def secretsFile = file(System.getenv("JARSIGN_SECRETS_FILE"))

    def secrets = new Properties()
    secrets.load(secretsFile.newReader())

    [jar, apiJar, serverJar].each { jarTask ->
        def task = task(jarTask.name + 'Sign', type: SignJar) {
            keyStore = System.getenv("JARSIGN_KEYSTORE_FILE")
            alias = secrets.alias
            storePass = secrets.storePassword
            keyPass = secrets.keyPassword

            inputFile = jarTask.archivePath
            outputFile = jarTask.archivePath
        }

        build.dependsOn task
    }
}

build.dependsOn apiJar
build.dependsOn serverJar
build.dependsOn gradle.includedBuilds*.task(':build')

